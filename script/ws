#!/usr/bin/env perl

use Mojolicious::Lite;

use lib 'lib';
use WS::Model::Users;

# Make signed cookies tamper resistant
app->secrets(['Mojolicious rocks']);

# Fix static paths
pop @{app->static->paths};
push @{app->static->paths} => '/Users/gene/sandbox/Mojo-Family/public',
                              '/Users/gene/sandbox/Mojo-Family/public/css';

# Helper to lazy initialize and store our model object
helper users => sub { state $users = WS::Model::Users->new };
 
my $clients = {};

websocket '/echo' => sub {
    my $c = shift;

    my $id = sprintf '%s', $c->tx;
    $clients->{$id} = $c->tx;

    $c->on(json => sub {
        my ($ctrl, $hash) = @_;

        $hash->{msg} =~ s/\n/<br>/g;

        for (keys %$clients) {
            $clients->{$_}->send({json => $hash});
        }
    });
};
 
any '/' => sub {
    my $c = shift;

    # Query parameters
    my $user = $c->param('user') || '';
    my $pass = $c->param('pass') || '';

    # Check password
    return $c->render
        unless $c->users->check($user, $pass);

    # Store username in session
    $c->session(user => $user);

    # Store a friendly message for the next page in flash
#    $c->flash(message => 'Thanks for logging in.');

    # Redirect to protected page with a 302 response
    $c->redirect_to('chat');
} => 'index';
 
# Make sure user is logged in for actions in this group
group {
    under sub {
        my $c = shift;
        # Redirect to main page with a 302 response if user is not logged in
        return 1 if $c->session('user');
        $c->redirect_to('index');
        return undef;
    };

    # A protected page
    get '/chat';
};

# Logout action
get '/logout' => sub {
    my $c = shift;

    # Expire and in turn clear session automatically
    $c->session(expires => 1);

    # Redirect to main page with a 302 response
    $c->redirect_to('index');
};

any '/chat' => 'chat';

app->start;
__DATA__

@@ index.html.ep
% layout 'default', title => 'Login';
<div class="container">
<h1><%= title %></h1>
%= form_for index => begin
    % if (param 'user') {
        <b>Wrong name or password, please try again.</b><br>
    % }
    %= text_field 'user', placeholder => 'Name'
    %= tag 'br'
    %= password_field 'pass', placeholder => 'Password'
    %= tag 'br'
    %= submit_button 'Login'
% end
</div>

@@ chat.html.ep
% layout 'default', title => 'Family Chat';
<div class="container">
<h1><%= title %></h1>
%= text_area 'chat', name => 'text', class => 'chat', id => 'chat'
%= tag 'br'
%= submit_button 'submit', name => 'submit', value => 'Chat', id => 'submit', class => 'button-primary'
%= tag 'br'
<div class="row">
    <div class="nine columns">
%= tag 'div', name => 'data', id => 'data'
    </div>
    <div class="three columns">
        <b>This month:</b>
%= tag 'br'
        <ul class="event">
        </ul>
    </div>
</div>
<script>
    var ws = new WebSocket('<%= url_for('echo')->to_abs %>');
    ws.onmessage = function (event) {
        $('#data').append('<p>' + JSON.parse(event.data).msg + '</p>');
    };
    ws.onopen = function (event) {
        console.log('ws.onopen');
    };
    $('#submit').click(function (e) {
        if ($('#chat').val()) {
            ws.send(JSON.stringify({msg: $('#chat').val()}));
            $('#chat').val('');
        }
    });
</script>
</div>


@@ layouts/default.html.ep
<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Transitional//EN"
        "http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd">
<html xmlns="http://www.w3.org/1999/xhtml">
<head>
    <title><%= title %></title>
    <meta http-equiv="Content-type" content="text/html; charset=utf-8" />
    <meta name="google" content="notranslate">
    <link rel="stylesheet" type="text/css" href="<%= url_for 'normalize.css' %>">
    <link rel="stylesheet" type="text/css" href="<%= url_for 'skeleton.css' %>">
    <link rel="stylesheet" type="text/css" href="<%= url_for 'style.css' %>">
    <script src="//ajax.googleapis.com/ajax/libs/jquery/3.3.1/jquery.min.js"></script>
</head>
<body>
<%= content %>
<div class="row brown">
<div class="four columns leftpad">
<%= link_to '/privacy.html' => begin %>Privacy policy<% end %>
</div>
<div class="four columns centerpad">
<%= link_to '/help.html' => begin %>Help<% end %>
</div>
<div class="four columns rightpad">
    Built by <a href="http://gene.ology.net/">Gene Boggs</a>
</div>
</div>
</body>
</html>
